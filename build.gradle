group = 'com.blueanvil'
version = '1.0.11'

buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        force "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    api "org.slf4j:slf4j-api:$slf4jVersion"
    api "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation "org.apache.kafka:kafka_2.12:$kafkaVersion"
    implementation "org.apache.kafka:kafka-clients:$kafkaVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.slf4j:slf4j-log4j12:$slf4jVersion"
}

jacoco {
    reportsDir = file("etc/code-coverage")
}

jacocoTestReport {
    additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

task runTools(type: Exec) {
    commandLine './start-tools.sh'
}

task stopTools(type: Exec) {
    commandLine './stop-tools.sh'
}

tasks.test.dependsOn("runTools")
test.finalizedBy(stopTools)


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar

            pom {
                name = 'Kotka'
                description = 'A simple and thin Kotlin wrapper for Kafka'
                url = 'https://github.com/blueanvil/kotka'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'cosmin-marginean'
                        name = 'Cosmin Marginean'
                        email = 'cos.marginean@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/blueanvil/kotka.git'
                    developerConnection = 'scm:git:ssh://github.com/blueanvil/kotka.git'
                    url = 'https://github.com/blueanvil/kotka'
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

